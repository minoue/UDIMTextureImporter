// GUI version 2.3.6

[VarDef, dllPath,""] //path to dll
[VarDef, err, 0] //standard error
[VarDef, fileName, ""]
[VarDef, udimPluginPath, ""]
[VarDef, dllLog, ""]
[VarDef, layerPath, ""]
[VarDef, subtoolNameOrig, ""]
[VarDef, currentSubdiv, 0]

[VarDef, GoZPathToDLL, ""]
[VarDef, objPathFromDLL, ""]
[VarDef, plyPathFromDLL, ""]
[VarDef, objPathUV, ""]
[VarDef, Gamma, 1.0]
[VarDef, midValue, 0.0]

// Import model
// 0 : Disabled
// 1 : Vector Displacement
// 2 : Normal
// 3 : Color
[VarDef, mode, 1]


[RoutineDef, CheckSystem,
    // Get zbrush version

	[VarSet, isMac, [ZBrushInfo, 6]]	// check Mac or PC
	// Make sure we have the dll and set its path

    [VarSet, GoZPathToDLL, [FileNameResolvePath, "ZPUBLIC_Temp/UDIMImporter_to_DLL.GoZ"]]
    [VarSet, objPathFromDLL, [FileNameResolvePath, "ZPUBLIC_Temp/UDIMImporter_from_DLL.obj"]]
    [VarSet, plyPathFromDLL, [FileNameResolvePath, "ZPUBLIC_Temp/UDIMImporter_from_DLL.ply"]]

    [If, isMac,
        // For release
        [VarSet, dllPath, "ZBRUSH_ZSTARTUP/ZPlugs64/UDIMTextureImporterData/ZFileUtils/ZFileUtils.lib"]
        [VarSet, udimPluginPath, "ZBRUSH_ZSTARTUP/ZPlugs64/UDIMTextureImporterData/UDIMTextureImporter.lib"]

    , // else, Windows
        [VarSet, dllPath, "ZBRUSH_ZSTARTUP\ZPlugs64\UDIMTextureImporterData\ZFileUtils\ZFileUtils64.dll"]
        [VarSet, udimPluginPath, "ZBRUSH_ZSTARTUP\ZPlugs64\UDIMTextureImporterData\UDIMTextureImporter.dll"]
    ]

    // Check ZFileUtils
	[If, [FileExists, [Var,dllPath]],
		//check that correct version
		[VarSet, dllVersion, [FileExecute, [Var,dllPath], Version]]
		[If, [Val,dllVersion] >= 1.0, //dll version
			//OK
			,//else earlier version
			[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ZFileUtils plugin DLL is an earlier version which does not support this plugin.  Please install correct version."]
			[Exit]
		]
	, // else no DLL.
		[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ZFileUtils plugin \CffffffDLL\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
		[Exit]
	]

    // Check displacementImporter plugin
	[If, [FileExists, [Var, udimPluginPath]],

    // If exists, good. do nothing

	, // else no DLL.
    [Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 DisplacementImporter plugin \CffffffDLL\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
    [Exit]
	]
]//end routine


//call routine here to ensure it's called every time plugin loaded
[RoutineCall, CheckSystem]


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ INTERFACE
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


[ISubPalette,"ZPlugin:UDIM Texture Importer"]
       

[ISwitch,
    "ZPlugin:UDIM Texture Importer:Vector Displacement",
    1,
    "Switch 1",
    // on command
    [IUnPress, "ZPlugin:UDIM Texture Importer:Normal Displacement"]
    [IUnPress, "ZPlugin:UDIM Texture Importer:Vertex Color"]
    [VarSet, mode, 1]
    ,
    // off command
    [VarSet, mode, 0]
    ,
    0, // Initially disabled?
    150, // Width
    25, // Height
    ]
[IEnable, "ZPlugin:UDIM Texture Importer:Vector Displacement"]

[ISwitch,
    "ZPlugin:UDIM Texture Importer:Normal Displacement",
    0,
    "Switch 1",
    // on command
    [IUnPress, "ZPlugin:UDIM Texture Importer:Vector Displacement"]
    [IUnPress, "ZPlugin:UDIM Texture Importer:Vertex Color"]
    [VarSet, mode, 2]
    ,
    // off command
    [VarSet, mode, 0]
    ,
    0, // Initially disabled?
    150, // Width
    25, // Height
    ]
[IEnable, "ZPlugin:UDIM Texture Importer:Normal Displacement"]

[ISwitch,
    "ZPlugin:UDIM Texture Importer:Vertex Color",
    0,
    "Switch 1",
    // on command
    [IUnPress, "ZPlugin:UDIM Texture Importer:Vector Displacement"]
    [IUnPress, "ZPlugin:UDIM Texture Importer:Normal Displacement"]
    [VarSet, mode, 3]
    ,
    // off command
    [VarSet, mode, 0]
    ,
    1, // Initially disabled?
    150, // Width
    25, // Height
    ]
[IEnable, "ZPlugin:UDIM Texture Importer:Vertex Color"]


// [ISlider,
//     "ZPlugin:UDIM Texture Importer:Gamma",
//     1, // initial value
//     0.1, // Resolution
//     1, // Min Value
//     2.2, // Max Value
//     , // Info text
//     [VarSet, Gamma, [IGet, "ZPlugin:UDIM Texture Importer:Gamma"]], // Command group
//     1,
//     75,
//     0
// ]
// [IEnable, "ZPlugin:UDIM Texture Importer:Gamma"]

[ISlider,
    "ZPlugin:UDIM Texture Importer:Mid Value",
    0, // initial value
    0.1, // Resolution
    0, // Min value
    1.0, // Max value
    , // Info text
    [VarSet, midValue, [IGet, "ZPlugin:UDIM Texture Importer:Mid Value"]], // Command group
    0,
    150,
    0
]
[IEnable, "ZPlugin:UDIM Texture Importer:Mid Value"]

[IButton,
    "ZPlugin:UDIM Texture Importer:Import UDIM Textures", // Button name
    "Import UDIM Textures", // Popup info text
        
        [If, mode == 0,
            [Note, "Select Import mode. Aborted"]
            [Exit]
            ,
        ]
        
        // Import textures
        [VarDef, dialogTitle, "Please select image files"]
        [VarSet, fileExt, "tif,tiff,exr,png,jpg,jpeg"]
        [MemCreate, ZFileUTils_FileExt, 256, 0]
        [MemWriteString, ZFileUTils_FileExt, fileExt, 0]
        [VarSet, fileCount, [FileExecute, [Var,dllPath], "GetFilesDialog", #dialogTitle, , ZFileUTils_FileExt]]

        [if, fileCount == 0,
            [Note, "No textures are selected. Aborted."]
            [MemDelete, ZFileUTils_FileExt]
            [Exit]
            ,
        ]
        
        // Cleanup
        [MemDelete, ZFileUTils_FileExt]
        
        // Store orig tool name
        [VarSet, subtoolNameOrig, [IGetTitle,Tool:ItemInfo]]

        // Save original UV data
        // Return obj file has no UVs so needs to export/import UV mesh to
        // update UVs at the end of the process
        [VarSet, objPathUV, [FileNameResolvePath, [StrMerge, "ZPUBLIC_Temp/", subtoolNameOrig, "obj"]]]
        [VarSet, currentSubdiv, [IGet, Tool:Geometry:SDiv]]
        [ISet,Tool:Geometry:SDiv,1]
        [FileNameSetNext, objPathUV]
        [IPress, Tool:Export]
        [ISet,Tool:Geometry:SDiv, [Var, currentSubdiv]]

        // Create new layer for displacement
        // no layer needed for color
        [If, mode != 3,
            [IPress, Tool:Layers:New]
            [IPress, Tool:Layers:Rename]
            [VarSet, layerPath, [StrMerge,"Tool:Layers:", [IGetTitle, "Tool:Layers:Layer Intensity"]]]
            , // else, do nothing
        ]

        // Export GoZ file
        [FileNameSetNext, GoZPathToDLL]
        [IPress, Tool:Export]

        [if, fileCount > 0,
            [MemCreate, memTexturePaths, 256, 0] // to store single texture path
            [MemCreate, memOutputPaths, 32768, 0]
            [MemCreate, memDllLog, 256, 0]

            [VarSet, index, 1]
            [VarSet, offset, 0]

            [MemWriteString, memOutputPaths, [StrMerge, mode, "#"], offset] 
            [VarInc, offset]
            [VarInc, offset]

            [Loop, fileCount,
                [VarSet, err, [FileExecute, [Var, dllPath], GetFilePath, , index, memTexturePaths]]
                [VarInc, index]
                [if, err == 0,
                    [MemReadString, memTexturePaths, fileName]

                    // Append texture path with "#" at the end of memOutputPaths
                    [MemWriteString, memOutputPaths, [StrMerge, fileName, "#"], offset]
                    [VarSet, offset, offset + [StrLength, fileName] + 1]
                    ,
                    [LoopContinue]
                ]
            ] // End loop

            [VarSet, err, [FileExecute,
                            [Var, udimPluginPath],
                            "ImportUDIM",
                            GoZPathToDLL,
                            midValue,
                            memOutputPaths,
                            memDllLog]]
            [If, err,
                [Note, "Failed to run the command in DLL."]
                [MemReadString, memDllLog, dllLog]
                [Note, [Var, dllLog]]
                [MemDelete, memTexturePaths]
                [MemDelete, memOutputPaths]
                [MemDelete, memDllLog]
                [Exit]
                ,
            ]

            [MemDelete, memTexturePaths]
            [MemDelete, memOutputPaths]
            [MemDelete, memDllLog]

        ] // End if fileCount

        [If, mode == 3,
            // Import color
            [If, [FileExists, plyPathFromDLL],
                [FileNameSetNext, plyPathFromDLL]
                [IPress,Tool:Import]
                , // else
                [Note, "ply file not found." , , 2]
                [Exit]
            ]
            , // else, import displacement
            [If, [FileExists, objPathFromDLL],
                [FileNameSetNext, objPathFromDLL]
                [IPress,Tool:Import]
                , // else
                [Note, "obj file not found." , , 2]
                [Exit]
            ]
        ]

        // Restore UVs
        [If, mode != 3,
            [ISet, [Var, layerPath] ,1]
            [ISet, [Var, layerPath] ,0]
            ,
        ]

        [ISet,Tool:Geometry:SDiv,1]
        [FileNameSetNext, objPathUV]
        [IPress,Tool:Import]
        [ISet,Tool:Geometry:SDiv, [Var, currentSubdiv]]

        [If, mode != 3,
            [ISet, [Var, layerPath] ,0]
            [ISet, [Var, layerPath] ,1]
            ,
        ]

        [MessageOK, "Done"],

    0, // Initially Disabled? (0: enabled)
    1, // Button width in pixels,
    , // optional button icon
    ,
    36 // height
]//end button
