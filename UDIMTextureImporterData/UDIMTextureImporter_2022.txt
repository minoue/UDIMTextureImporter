// GUI version 2.2.3

[VarDef, dllPath,""] //path to dll
[VarDef, err, 0] //standard error
[VarDef, fileName, ""]
[VarDef, udimPluginPath, ""]
[VarDef, dllLog, ""]
[VarDef, layerPath, ""]
[VarDef, subtoolNameOrig, ""]
[VarDef, currentSubdiv, 0]

[VarDef, GoZPathToDLL, ""]
[VarDef, GoZPathFromDLL, ""]
[VarDef, objPathFromDLL, ""]
[VarDef, objPathUV, ""]
[VarDef, Gamma, 1.0]

// Import model
// 0 : Disabled
// 1 : Vector Displacement
// 2 : Normal
// 3 : Color
// 4 : Mask
[VarDef, mode, 1]


[RoutineDef, CheckSystem,
    // Get zbrush version

	[VarSet, isMac, [ZBrushInfo, 6]]	// check Mac or PC
	// Make sure we have the dll and set its path

    [VarSet, GoZPathToDLL, [FileNameResolvePath, "ZPUBLIC_Temp/dspImporter_to_DLL.GoZ"]]
    [VarSet, GoZPathFromDLL, [FileNameResolvePath, "ZPUBLIC_Temp/dspImporter_from_DLL.GoZ"]]
    [VarSet, objPathFromDLL, [FileNameResolvePath, "ZPUBLIC_Temp/dspImporter_from_DLL.obj"]]
    // [VarSet, objPathUV, [FileNameResolvePath, "ZPUBLIC_Temp/tempUVMesh.obj"]]

    [If, isMac,
        // For release
        [VarSet, dllPath, "ZBRUSH_ZSTARTUP/ZPlugs64/UDIMTextureImporterData/ZFileUtils/ZFileUtils.lib"]
        [VarSet, udimPluginPath, "ZBRUSH_ZSTARTUP/ZPlugs64/UDIMTextureImporterData/UDIMTextureImporter.lib"]

    , // else, Windows
        [VarSet, dllPath, "ZBRUSH_ZSTARTUP\ZPlugs64\UDIMTextureImporterData\ZFileUtils\ZFileUtils64.dll"]
        [VarSet, udimPluginPath, "ZBRUSH_ZSTARTUP\ZPlugs64\UDIMTextureImporterData\UDIMTextureImporter.dll"]
    ]

    // Check ZFileUtils
	[If, [FileExists, [Var,dllPath]],
		//check that correct version
		[VarSet, dllVersion, [FileExecute, [Var,dllPath], Version]]
		[If, [Val,dllVersion] >= 1.0, //dll version
			//OK
			,//else earlier version
			[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ZFileUtils plugin DLL is an earlier version which does not support this plugin.  Please install correct version."]
			[Exit]
		]
	, // else no DLL.
		[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ZFileUtils plugin \CffffffDLL\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
		[Exit]
	]

    // Check displacementImporter plugin
	[If, [FileExists, [Var, udimPluginPath]],

    // If exists, good. do nothing

	, // else no DLL.
    [Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 DisplacementImporter plugin \CffffffDLL\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
    [Exit]
	]
]//end routine


//call routine here to ensure it's called every time plugin loaded
[RoutineCall, CheckSystem]


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ INTERFACE
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


[ISubPalette,"ZPlugin:UDIM Texture Importer"]
       

[ISwitch,
    "ZPlugin:UDIM Texture Importer:Vector Displacement",
    1,
    "Switch 1",
    // on command
    [IUnPress, "ZPlugin:UDIM Texture Importer:Normal Displacement"]
    // [IUnPress, "ZPlugin:UDIM Texture Importer:Vertex Color"]
    // [IUnPress, "ZPlugin:UDIM Texture Importer:Mask"]
    [VarSet, mode, 1]
    ,
    // off command
    [VarSet, mode, 0]
    ,
    0, // Initially disabled?
    150, // Width
    25, // Height
    ]
[IEnable, "ZPlugin:UDIM Texture Importer:Vector Displacement"]

[ISwitch,
    "ZPlugin:UDIM Texture Importer:Normal Displacement",
    0,
    "Switch 1",
    // on command
    [IUnPress, "ZPlugin:UDIM Texture Importer:Vector Displacement"]
    // [IUnPress, "ZPlugin:UDIM Texture Importer:Vertex Color"]
    // [IUnPress, "ZPlugin:UDIM Texture Importer:Mask"]
    [VarSet, mode, 2]
    ,
    // off command
    [VarSet, mode, 0]
    ,
    0, // Initially disabled?
    150, // Width
    25, // Height
    ]
[IEnable, "ZPlugin:UDIM Texture Importer:Normal Displacement"]

[ISwitch,
    "ZPlugin:UDIM Texture Importer:Vertex Color",
    0,
    "Switch 1",
    // on command
    [IUnPress, "ZPlugin:UDIM Texture Importer:Vector Displacement"]
    [IUnPress, "ZPlugin:UDIM Texture Importer:Normal Displacement"]
    [IUnPress, "ZPlugin:UDIM Texture Importer:Mask"]
    [VarSet, mode, 3]
    ,
    // off command
    [VarSet, mode, 0]
    ,
    1, // Initially disabled?
    150, // Width
    25, // Height
    ]
// [IEnable, "ZPlugin:UDIM Texture Importer:Vertex Color"]

[ISwitch,
    "ZPlugin:UDIM Texture Importer:Mask",
    0,
    "Switch 1",
    // on command
    [IUnPress, "ZPlugin:UDIM Texture Importer:Vector Displacement"]
    [IUnPress, "ZPlugin:UDIM Texture Importer:Normal Displacement"]
    [IUnPress, "ZPlugin:UDIM Texture Importer:Vertex Color"]
    [VarSet, mode, 4]
    ,
    // off command
    [VarSet, mode, 0]
    ,
    1, // Initially disabled?
    150, // Width
    25, // Height
    ]
// [IEnable, "ZPlugin:UDIM Texture Importer:Mask"]

[ISlider,
    "ZPlugin:UDIM Texture Importer:Gamma",
    1,
    0.1,
    0,
    2.2,
    ,
    // command
    [VarSet, Gamma, [IGet, "ZPlugin:UDIM Texture Importer:Gamma"]]
]
[IEnable, "ZPlugin:UDIM Texture Importer:Gamma"]

[IButton,
    "ZPlugin:UDIM Texture Importer:Import UDIM Textures", // Button name
    "Import UDIM Textures", // Popup info text
        
        [If, mode == 0,
            [Note, "Select Import mode. Aborted"]
            [Exit]
            ,
        ]
        
        // Import textures
        [VarDef, dialogTitle, "Please select image files"]
        [VarSet, fileExt, "tif,tiff,exr,png,jpg,jpeg"]
        [MemCreate, ZFileUTils_FileExt, 256, 0]
        [MemWriteString, ZFileUTils_FileExt, fileExt, 0]
        [VarSet, fileCount, [FileExecute, [Var,dllPath], "GetFilesDialog", #dialogTitle, , ZFileUTils_FileExt]]

        [if, fileCount == 0,
            [Note, "No textures are selected. Aborted."]
            [MemDelete, ZFileUTils_FileExt]
            [Exit]
            ,
        ]
        
        // Cleanup
        [MemDelete, ZFileUTils_FileExt]
        
        // Store orig tool name
        [VarSet, subtoolNameOrig, [IGetTitle,Tool:ItemInfo]]
        [VarSet, objPathUV, [FileNameResolvePath, [StrMerge, "ZPUBLIC_Temp/", subtoolNameOrig, "obj"]]]

        // Export temp UV mesh
        // Return obj file has no UVs so needs to export/import UV mesh to update 
        // UVs at the end of the process
        [VarSet, currentSubdiv, [IGet, Tool:Geometry:SDiv]]
        [ISet,Tool:Geometry:SDiv,1]
        [FileNameSetNext, objPathUV]
        [IPress, Tool:Export]
        [ISet,Tool:Geometry:SDiv, [Var, currentSubdiv]]

        // Create new layer
        [IPress, Tool:Layers:New]
        [IPress, Tool:Layers:Rename]
        [VarSet, layerPath, [StrMerge,"Tool:Layers:", [IGetTitle, "Tool:Layers:Layer Intensity"]]]

        // Export GoZ file
        [FileNameSetNext, GoZPathToDLL]
        [IPress, Tool:Export]

        [if, fileCount > 0,
            [MemCreate, memTexturePaths, 256, 0] // to store single texture path
            [MemCreate, memOutputPaths, 32768, 0]
            [MemCreate, memDllLog, 256, 0]

            [VarSet, index, 1]
            [VarSet, offset, 0]

            [MemWriteString, memOutputPaths, [StrMerge, mode, "#"], offset] 
            [VarInc, offset]
            [VarInc, offset]

            [Loop, fileCount,
                [VarSet, err, [FileExecute, [Var, dllPath], GetFilePath, , index, memTexturePaths]]
                [VarInc, index]
                [if, err == 0,
                    [MemReadString, memTexturePaths, fileName]

                    // Append texture path with "#" at the end of memOutputPaths
                    [MemWriteString, memOutputPaths, [StrMerge, fileName, "#"], offset]
                    [VarSet, offset, offset + [StrLength, fileName] + 1]
                    ,
                    [LoopContinue]
                ]
            ] // End loop

            [VarSet, err, [FileExecute,
                            [Var, udimPluginPath],
                            "ImportUDIM",
                            GoZPathToDLL,
                            Gamma,
                            memOutputPaths,
                            memDllLog]]
            [If, err,
                [Note, "An error has occured"]
                [MemReadString, memDllLog, dllLog]
                [Note, [Var, dllLog]]
                [MemDelete, memTexturePaths]
                [MemDelete, memOutputPaths]
                [MemDelete, memDllLog]
                [Exit]
                ,
            ]

            [MemDelete, memTexturePaths]
            [MemDelete, memOutputPaths]
            [MemDelete, memDllLog]

        ] // End if fileCount


        // [FileNameSetNext, GoZPathFromDLL]
        [If, [FileExists, objPathFromDLL],
            [FileNameSetNext, objPathFromDLL]
            [IPress,Tool:Import]
            , // else
            [Note, "obj file not found." , , 2]
            [Exit]
        ]

        [ISet, [Var, layerPath] ,1]
        [ISet, [Var, layerPath] ,0]
        [ISet,Tool:Geometry:SDiv,1]
        [FileNameSetNext, objPathUV]
        [IPress,Tool:Import]
        [ISet,Tool:Geometry:SDiv, [Var, currentSubdiv]]
        [ISet, [Var, layerPath] ,0]
        [ISet, [Var, layerPath] ,1]

        // Update view for mask
        [If, mode == 4,
            [IPress,Tool:Masking:Inverse]
            [IPress,Tool:Masking:Inverse]
            ,
        ]

        // Cleanup
        [If, [FileExists, GoZPathFromDLL],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", #GoZPathFromDLL]] // Delete this file...
            [If, err,
                [Note, "Failed to delete the sculpted mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            // [Note, "No file to delete!" , , 2]
        ]

        [If, [FileExists, GoZPathToDLL],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", #GoZPathToDLL]] // Delete this file...
            [If, err,
                [Note, "Failed to delete the raw mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            // [Note, "No file to delete!" , , 2]
        ]
        
        [If, [FileExists, [StrMerge, "!:", [Var, GoZPathToDLL]]],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", [StrMerge, "!:", [Var, objPathToDLL]]]]
            [If, err,
                [Note, "Failed to delete the raw mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            // [Note, "No file to delete!" , , 2]
        ]

        [MessageOK, "Done"],

    0, // Initially Disabled? (0: enabled)
    1, // Button width in pixels,
    , // optional button icon
    ,
    36 // height
]//end button
