
[VarDef, dllPath,""] //path to dll
[VarDef, err, 0] //standard error
[VarDef, fileName, ""]
[VarDef, udimPluginPath, ""]
[VarDef, dllLog, ""]
[VarDef, objPathOut, ""]
[VarDef, objPathIn, ""]


[RoutineDef, CheckSystem,
	[VarSet, isMac, [ZBrushInfo, 6]]	// check Mac or PC
	// Make sure we have the dll and set its path

    [If, isMac,
        // For test
        [VarSet, dllPath, "ZFileUtils/ZFileUtils.lib"]
        [VarSet, udimPluginPath, "displacementImporter.lib"]

        // For release
        // [VarSet, dllPath, "ZBRUSH_ZSTARTUP/ZPlugs64/UDIMImporter/ZFileUtils/ZFileUtils.lib"]
        // [VarSet, udimPluginPath, "ZBRUSH_ZSTARTUP/ZPlugs64/UDIMImporter/displacementImporter.lib"]

        [VarSet, objPathOut, "/tmp/ZBrush_displacementImporter_out.obj"]
        [VarSet, objPathIn, "!:/tmp/ZBrush_displacementImporter_in.obj"]

    , // else, Windows

        [Note, "Windows is not supported yet."]
        [Exit]
        // For test
        // [VarSet, dllPath, "ZFileUtils\ZFileUtils64.dll"]
        // [VarSet, udimPluginPath, "displacementImporter.dll"]

        // For Release
        // [VarSet, dllPath, "ZBRUSH_ZSTARTUP\ZPlugs64\MyPluginData\ZFileUtils64.dll"]
        // [VarSet, udimPluginPath, "ZBRUSH_ZSTARTUP/ZPlugs64/UDIMImporter/displacementImporter.dll"]

        // [VarSet, objPathOut, "/tmp/ZBrush_displacementImporter_out.obj"]
        // [VarSet, objPathIn, "!:/tmp/ZBrush_displacementImporter_in.obj"]
    ]

    // Check ZFileUtils
	[If, [FileExists, [Var,dllPath]],
		//check that correct version
		[VarSet, dllVersion, [FileExecute, [Var,dllPath], Version]]
		[If, [Val,dllVersion] >= 1.0, //dll version
			//OK
			,//else earlier version
			[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ZFileUtils plugin DLL is an earlier version which does not support this plugin.  Please install correct version."]
			[Exit]
		]
	, // else no DLL.
		[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ZFileUtils plugin \CffffffDLL\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
		[Exit]
	]

    // Check displacementImporter plugin
	[If, [FileExists, [Var, udimPluginPath]],

    // If exists, good. do nothing

	, // else no DLL.
		[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 DisplacementImporter plugin \CffffffDLL\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
		[Exit]
	]
]//end routine


//call routine here to ensure it's called every time plugin loaded
[RoutineCall, CheckSystem]


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ INTERFACE
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


[ISubPalette,"ZPlugin:UDIM Importer"]


[IButton,
    "ZPlugin:UDIM Importer:Import Displacement", // Button name
    "Test button", // Popup info text

        // Export obj
        [FileNameSetNext, objPathOut]
        [IPress, Tool:Export]

        [VarDef, dialogTitle, "Please select image files"]
        [VarSet, fileExt, "tif,tiff,exr"]
        [MemCreate, ZFileUTils_FileExt, 256, 0]
        [MemWriteString, ZFileUTils_FileExt, fileExt, 0]
        [VarSet, fileCount, [FileExecute, [Var,dllPath], "GetFilesDialog", #dialogTitle, , ZFileUTils_FileExt]]

        // Cleanup
        [MemDelete, ZFileUTils_FileExt]

        [if, fileCount == 0,
            [Note, "No textures are selected."]
            ,
        ]

        [if, fileCount > 0,
            [MemCreate, memTexturePaths, 256, 0] // to store single texture path
            [MemCreate, memOutputPaths, 1024, 0]
            [MemCreate, memDllLog, 256, 0]

            [VarSet, index, 1]
            [VarSet, offset, 0]

            [Loop, fileCount,
                [VarSet, err, [FileExecute, [Var, dllPath], GetFilePath, , index, memTexturePaths]]
                [VarInc, index]
                [if, err == 0,
                    [MemReadString, memTexturePaths, fileName]

                    // Append texture path with ":" at the end of memOutputPaths
                    [MemWriteString, memOutputPaths, [StrMerge, fileName, ":"], offset]
                    [VarSet, offset, offset + [StrLength, fileName] + 1]
                    ,
                    [LoopContinue]
                ]
            ] // End loop

            [VarSet, err, [FileExecute,
                            [Var, udimPluginPath],
                            "ImportVectorDisplacement",
                            objPathOut,
                            offset,
                            memOutputPaths,
                            memDllLog]]
            [If, err,
                // [Note, "An error has occured"]
                [MemReadString, memDllLog, dllLog]
                [Note, [Var, dllLog]]
                [MemDelete, memTexturePaths]
                [MemDelete, memOutputPaths]
                [MemDelete, memDllLog]
                [Exit]
                ,
            ]

            [MemDelete, memTexturePaths]
            [MemDelete, memOutputPaths]
            [MemDelete, memDllLog]

        ] // End if fileCount

        [FileNameSetNext, objPathIn]
        [IPress,Tool:Import]

        // Cleanup
        [If, [FileExists, objPathIn],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", #objPathIn]] // Delete this file...
            [If, err,
                [Note, "Failed to delete the sculpted mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            [Note, "No file to delete!" , , 2]
        ]

        [VarDef, objPathOutFullPath, [StrMerge, "!:", [Var, objPathOut]]]
        [If, [FileExists, objPathOutFullPath],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", #objPathOutFullPath]] // Delete this file...
            [If, err,
                [Note, "Failed to delete the raw mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            [Note, "No file to delete!" , , 2]
        ]

        [MessageOK, "Done"],

    0, // Initially Disabled? (0: enabled)
    1, // Button width in pixels,
    ,
    ,
    36 // height
]//end button
