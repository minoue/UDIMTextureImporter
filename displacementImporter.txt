
[VarDef, dllPath,""] //path to dll
[VarDef, err, 0] //standard error
[VarDef, fileName, ""]
[VarDef, udimPluginPath, ""]
[VarDef, dllLog, ""]

[VarDef, objPathIn, ""]
[VarDef, objPathOut, ""]

[VarDef, zbrushVersion, 0]


[RoutineDef, CheckSystem,
    // Get zbrush version
	[VarSet, zbrushVersion, [ZBrushInfo, 0]]	// check Mac or PC

	[VarSet, isMac, [ZBrushInfo, 6]]	// check Mac or PC
	// Make sure we have the dll and set its path

    [If, isMac,
        // For release
        [VarSet, dllPath, "ZBRUSH_ZSTARTUP/ZPlugs64/displacementImporter/ZFileUtils/ZFileUtils.lib"]
        [VarSet, udimPluginPath, "ZBRUSH_ZSTARTUP/ZPlugs64/displacementImporter/displacementImporter.lib"]

        [VarSet, objPathOut, "/Users/Shared/Pixologic/ZBrush_displacementImporter_out.obj"]
        [VarSet, objPathIn, "!:/Users/Shared/Pixologic/ZBrush_displacementImporter_in.obj"]

        // For test
        // [VarSet, dllPath, "ZFileUtils/ZFileUtils.lib"]
        // [VarSet, udimPluginPath, "displacementImporter.lib"]
    , // else, Windows
        // For Release
        [VarSet, dllPath, "ZBRUSH_ZSTARTUP\ZPlugs64\MyPluginData\ZFileUtils64.dll"]
        [VarSet, udimPluginPath, "ZBRUSH_ZSTARTUP\ZPlugs64\UDIMImporter\displacementImporter.dll"]

        [VarSet, objPathOut, "C:\Users\Public\Pixologic\ZBrush_displacementImporter_out.obj"]
        [VarSet, objPathIn, "C:\Users\Public\Pixologic\ZBrush_displacementImporter_in.obj"]
        
        // For test
        // [VarSet, dllPath, "ZFileUtils\ZFileUtils64.dll"]
        // [VarSet, udimPluginPath, "displacementImporter.dll"]
    ]

    // Check ZFileUtils
	[If, [FileExists, [Var,dllPath]],
		//check that correct version
		[VarSet, dllVersion, [FileExecute, [Var,dllPath], Version]]
		[If, [Val,dllVersion] >= 1.0, //dll version
			//OK
			,//else earlier version
			[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ZFileUtils plugin DLL is an earlier version which does not support this plugin.  Please install correct version."]
			[Exit]
		]
	, // else no DLL.
		[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ZFileUtils plugin \CffffffDLL\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
		[Exit]
	]

    // Check displacementImporter plugin
	[If, [FileExists, [Var, udimPluginPath]],

    // If exists, good. do nothing

	, // else no DLL.
		[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 DisplacementImporter plugin \CffffffDLL\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
		[Exit]
	]
]//end routine


//call routine here to ensure it's called every time plugin loaded
[RoutineCall, CheckSystem]


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ INTERFACE
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


[ISubPalette,"ZPlugin:UDIM Importer"]


[IButton,
    "ZPlugin:UDIM Importer:Import Displacement", // Button name
    "Test button", // Popup info text

        [VarDef, dialogTitle, "Please select image files"]
        [VarSet, fileExt, "tif,tiff,exr"]
        [MemCreate, ZFileUTils_FileExt, 256, 0]
        [MemWriteString, ZFileUTils_FileExt, fileExt, 0]
        [VarSet, fileCount, [FileExecute, [Var,dllPath], "GetFilesDialog", #dialogTitle, , ZFileUTils_FileExt]]

        [if, fileCount == 0,
            [Note, "No textures are selected. Aborted."]
            [MemDelete, ZFileUTils_FileExt]
            [Exit]
            ,
        ]

        // Export obj
        [FileNameSetNext, objPathOut]
        [IPress, Tool:Export]

        // Cleanup
        [MemDelete, ZFileUTils_FileExt]

        [if, fileCount > 0,
            [MemCreate, memTexturePaths, 256, 0] // to store single texture path
            [MemCreate, memOutputPaths, 1024, 0]
            [MemCreate, memDllLog, 256, 0]

            [VarSet, index, 1]
            [VarSet, offset, 0]

            [Loop, fileCount,
                [VarSet, err, [FileExecute, [Var, dllPath], GetFilePath, , index, memTexturePaths]]
                [VarInc, index]
                [if, err == 0,
                    [MemReadString, memTexturePaths, fileName]

                    // Append texture path with ":" at the end of memOutputPaths
                    [MemWriteString, memOutputPaths, [StrMerge, fileName, "#"], offset]
                    [VarSet, offset, offset + [StrLength, fileName] + 1]
                    ,
                    [LoopContinue]
                ]
            ] // End loop

            [VarSet, err, [FileExecute,
                            [Var, udimPluginPath],
                            "ImportVectorDisplacement",
                            objPathOut,
                            zbrushVersion,
                            memOutputPaths,
                            memDllLog]]
            [If, err,
                // [Note, "An error has occured"]
                [MemReadString, memDllLog, dllLog]
                [Note, [Var, dllLog]]
                [MemDelete, memTexturePaths]
                [MemDelete, memOutputPaths]
                [MemDelete, memDllLog]
                [Exit]
                ,
            ]

            [MemDelete, memTexturePaths]
            [MemDelete, memOutputPaths]
            [MemDelete, memDllLog]

        ] // End if fileCount

        [FileNameSetNext, objPathIn]
        [IPress,Tool:Import]

        // Cleanup
        [If, [FileExists, objPathIn],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", #objPathIn]] // Delete this file...
            [If, err,
                [Note, "Failed to delete the sculpted mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            // [Note, "No file to delete!" , , 2]
        ]

        [If, [FileExists, objPathOut],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", #objPathOut]] // Delete this file...
            [If, err,
                [Note, "Failed to delete the raw mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            // [Note, "No file to delete!" , , 2]
        ]
        
        [If, [FileExists, [StrMerge, "!:", [Var, objPathOut]]],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", [StrMerge, "!:", [Var, objPathOut]]]]
            [If, err,
                [Note, "Failed to delete the raw mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            // [Note, "No file to delete!" , , 2]
        ]

        [MessageOK, "Done"],

    0, // Initially Disabled? (0: enabled)
    1, // Button width in pixels,
    ,
    ,
    36 // height
]//end button
