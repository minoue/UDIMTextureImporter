
[VarDef, dllPath,""] //path to dll
[VarDef, err, 0] //standard error
[VarDef, fileName, ""]
[VarDef, udimPluginPath, ""]
[VarDef, dllLog, ""]

[VarDef, GoZPathToDLL, ""]
[VarDef, GoZPathFromDLL, ""]

// Import model
// 0 : Disabled
// 1 : Tangent Vector
// 2 : Normal
// 3 : Color
[VarDef, mode, 1]


[RoutineDef, CheckSystem,
    // Get zbrush version

	[VarSet, isMac, [ZBrushInfo, 6]]	// check Mac or PC
	// Make sure we have the dll and set its path

    [VarSet, GoZPathToDLL, [FileNameResolvePath, "ZPUBLIC_Temp/dspImporter_to_DLL.GoZ"]]
    [VarSet, GoZPathFromDLL, [FileNameResolvePath, "ZPUBLIC_Temp/dspImporter_from_DLL.GoZ"]]

    [If, isMac,
        // For release
        [VarSet, dllPath, "ZBRUSH_ZSTARTUP/ZPlugs64/DisplacementImporterData/ZFileUtils/ZFileUtils.lib"]
        [VarSet, udimPluginPath, "ZBRUSH_ZSTARTUP/ZPlugs64/DisplacementImporterData/displacementImporter.lib"]

    , // else, Windows
        [VarSet, dllPath, "ZBRUSH_ZSTARTUP\ZPlugs64\DisplacementImporterData\ZFileUtils\ZFileUtils64.dll"]
        [VarSet, udimPluginPath, "ZBRUSH_ZSTARTUP\ZPlugs64\DisplacementImporterData\displacementImporter.dll"]
    ]

    // Check ZFileUtils
	[If, [FileExists, [Var,dllPath]],
		//check that correct version
		[VarSet, dllVersion, [FileExecute, [Var,dllPath], Version]]
		[If, [Val,dllVersion] >= 1.0, //dll version
			//OK
			,//else earlier version
			[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ZFileUtils plugin DLL is an earlier version which does not support this plugin.  Please install correct version."]
			[Exit]
		]
	, // else no DLL.
		[Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ZFileUtils plugin \CffffffDLL\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
		[Exit]
	]

    // Check displacementImporter plugin
	[If, [FileExists, [Var, udimPluginPath]],

    // If exists, good. do nothing

	, // else no DLL.
    [Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 DisplacementImporter plugin \CffffffDLL\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
    [Exit]
	]
]//end routine


//call routine here to ensure it's called every time plugin loaded
[RoutineCall, CheckSystem]


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ INTERFACE
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


[ISubPalette,"ZPlugin:Displacement Importer"]
       
[ISwitch,
    "ZPlugin:Displacement Importer:Tangent Vector",
    1,
    "Switch 1",
    // on command
    [IUnPress, "ZPlugin:Displacement Importer:Normal Vector"]
    [IUnPress, "ZPlugin:Displacement Importer:Vertex Color"]
    [VarSet, mode, 1]
    ,
    // off command
    [VarSet, mode, 0]
    ,
    0, // Initially disabled?
    150, // Width
    25, // Height
    ]
[IEnable, "ZPlugin:Displacement Importer:Tangent Vector"]

[ISwitch,
    "ZPlugin:Displacement Importer:Normal Vector",
    0,
    "Switch 1",
    // on command
    [IUnPress, "ZPlugin:Displacement Importer:Tangent Vector"]
    [IUnPress, "ZPlugin:Displacement Importer:Vertex Color"]
    [VarSet, mode, 2]
    ,
    // off command
    [VarSet, mode, 0]
    ,
    0, // Initially disabled?
    150, // Width
    25, // Height
    ]
[IEnable, "ZPlugin:Displacement Importer:Normal Vector"]

[ISwitch,
    "ZPlugin:Displacement Importer:Vertex Color",
    0,
    "Switch 1",
    // on command
    [IUnPress, "ZPlugin:Displacement Importer:Tangent Vector"]
    [IUnPress, "ZPlugin:Displacement Importer:Normal Vector"]
    [VarSet, mode, 3]
    ,
    // off command
    [VarSet, mode, 0]
    ,
    0, // Initially disabled?
    150, // Width
    25, // Height
    ]
[IEnable, "ZPlugin:Displacement Importer:Vertex Color"]

[IButton,
    "ZPlugin:Displacement Importer:Import UDIM", // Button name
    "Import UDIM Textures", // Popup info text
        
        [If, mode == 0,
            [Note, "Select Import mode. Aborted"]
            [Exit]
            ,
        ]

        [VarDef, dialogTitle, "Please select image files"]
        [VarSet, fileExt, "tif,tiff,exr"]
        [MemCreate, ZFileUTils_FileExt, 256, 0]
        [MemWriteString, ZFileUTils_FileExt, fileExt, 0]
        [VarSet, fileCount, [FileExecute, [Var,dllPath], "GetFilesDialog", #dialogTitle, , ZFileUTils_FileExt]]

        [if, fileCount == 0,
            [Note, "No textures are selected. Aborted."]
            [MemDelete, ZFileUTils_FileExt]
            [Exit]
            ,
        ]

        // Export GoZ file
        [FileNameSetNext, GoZPathToDLL]
        [IPress, Tool:Export]

        // Cleanup
        [MemDelete, ZFileUTils_FileExt]

        // Get current position
        [VarDef, posX, 0]
        [VarDef, posY, 0]
        [VarDef, posZ, 0]
        [VarSet, posX, [IGet,Tool:Geometry:X Position]]
        [VarSet, posY, [IGet,Tool:Geometry:Y Position]]
        [VarSet, posZ, [IGet,Tool:Geometry:Z Position]]
        

        [if, fileCount > 0,
            [MemCreate, memTexturePaths, 256, 0] // to store single texture path
            [MemCreate, memOutputPaths, 2048, 0]
            [MemCreate, memDllLog, 256, 0]

            [VarSet, index, 1]
            [VarSet, offset, 0]

            [Loop, fileCount,
                [VarSet, err, [FileExecute, [Var, dllPath], GetFilePath, , index, memTexturePaths]]
                [VarInc, index]
                [if, err == 0,
                    [MemReadString, memTexturePaths, fileName]

                    // Append texture path with "#" at the end of memOutputPaths
                    [MemWriteString, memOutputPaths, [StrMerge, fileName, "#"], offset]
                    [VarSet, offset, offset + [StrLength, fileName] + 1]
                    ,
                    [LoopContinue]
                ]
            ] // End loop

            [VarSet, err, [FileExecute,
                            [Var, udimPluginPath],
                            "ImportDisplacement",
                            GoZPathToDLL,
                            mode,
                            memOutputPaths,
                            memDllLog]]
            [If, err,
                // [Note, "An error has occured"]
                [MemReadString, memDllLog, dllLog]
                [Note, [Var, dllLog]]
                [MemDelete, memTexturePaths]
                [MemDelete, memOutputPaths]
                [MemDelete, memDllLog]
                [Exit]
                ,
            ]

            [MemDelete, memTexturePaths]
            [MemDelete, memOutputPaths]
            [MemDelete, memDllLog]

        ] // End if fileCount


        [FileNameSetNext, GoZPathFromDLL]
        [IPress,Tool:Import]

        // Re-set position
        [ISet, Tool:Geometry:X Position, [Var, posX]]
        [ISet, Tool:Geometry:Y Position, [Var, posY]]
        [ISet, Tool:Geometry:Z Position, [Var, posZ]]

        // Cleanup
        [If, [FileExists, GoZPathFromDLL],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", #GoZPathFromDLL]] // Delete this file...
            [If, err,
                [Note, "Failed to delete the sculpted mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            // [Note, "No file to delete!" , , 2]
        ]

        [If, [FileExists, GoZPathToDLL],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", #GoZPathToDLL]] // Delete this file...
            [If, err,
                [Note, "Failed to delete the raw mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            // [Note, "No file to delete!" , , 2]
        ]
        
        [If, [FileExists, [StrMerge, "!:", [Var, GoZPathToDLL]]],
            [VarSet, err, [FileExecute, [Var, dllPath], "FileDelete", [StrMerge, "!:", [Var, objPathToDLL]]]]
            [If, err,
                [Note, "Failed to delete the raw mesh." , , 2]
            , //else OK
                // Do nothing
            ]
        , // else no file
            // [Note, "No file to delete!" , , 2]
        ]

        [MessageOK, "Done"],

    0, // Initially Disabled? (0: enabled)
    1, // Button width in pixels,
    , // optional button icon
    ,
    36 // height
]//end button
